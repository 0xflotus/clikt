apply plugin: 'org.jetbrains.dokka'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'

version = VERSION_NAME
group = GROUP

buildscript {
    repositories {
        jcenter()
    }

    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
    }
}

dokka {
    outputDirectory = "$rootDir/docs/api"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task dokkaJavadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
    outputFormat = 'javadoc'
    outputDirectory = javadoc.destinationDir
}

task javadocJar(type: Jar, dependsOn: dokkaJavadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

install {
    repositories.mavenInstaller {
        pom.project {
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection "https://github.com/ajalt/clikt.git"
                url "https://github.com/ajalt/clikt"
            }
            developers {
                developer {
                    name 'AJ Alt'
                }
            }
        }
    }
}

bintray {
    user = project.hasProperty('bintray.user') ? project.property('bintray.user') : ''
    key = project.hasProperty('bintray.apikey') ? project.property('bintray.apikey') : ''
    configurations = ['archives']
    pkg {
        repo = 'maven'
        name = 'clikt'
        licenses = ['Apache-2.0']
        vcsUrl = "https://github.com/ajalt/clikt.git"

        version {
            name = project.version
            desc = project.description
            released = new Date()
            vcsTag = project.version
        }
    }
}

